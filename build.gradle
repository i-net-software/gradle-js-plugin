plugins {
    id "com.gradle.plugin-publish" version "0.12.0"
    id "java-gradle-plugin"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'project-report'

sourceCompatibility = 11
targetCompatibility = 11

defaultTasks 'clean', 'build'

version = '2.18.0'
group = 'de.inetsoftware.gradle'
ext.archivesBaseName = 'gradle-js-plugin'
ext.isSnapshot = version.endsWith("-SNAPSHOT")

repositories {
    mavenCentral()
}

task createClasspathManifest {
    def outputDir = sourceSets.test.output.resourcesDir

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    implementation gradleApi()
    implementation('com.google.javascript:closure-compiler:v20250407') {
        exclude module: 'junit'
    }
    implementation('io.jdev.html2js:html2js:0.1') {
        exclude module: 'groovy-all'
    }
    testImplementation gradleTestKit()
    testImplementation files(createClasspathManifest)
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0')
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'commons-lang:commons-lang:2.6'
}

compileGroovy {
    options.compilerArgs = ['-Xlint:deprecation', '-Xlint:unchecked']
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    archiveClassifier = 'sources'
    from sourceSets.main.groovy
}

task groovydocJar(type: Jar) {
    description = 'An archive of the GroovyDocs for Maven Central'
    archiveClassifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourceJar
}

publishing {
    publications {
        maven(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }

            artifact sourceJar
            artifact groovydocJar
        }
    }
}

gradlePlugin {
    plugins {
        jsPlugin {
            id = 'com.cloudzilla.gradle.js'
            implementationClass = 'com.eriwen.gradle.js.JsPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/mampcs/gradle-js-plugin/'
    vcsUrl = 'https://github.com/mampcs/gradle-js-plugin/'
    description = 'Gradle plugin for working with JS'
    tags = ['javascript', 'jshint', 'requirejs']

    plugins {
        jsPlugin {
            id = 'de.inetsoftware.gradle.js'
            displayName = 'Gradle JavaScript Plugin'
        }
    }
    mavenCoordinates {
        groupId = 'de.inetsoftware.gradle'
        artifactId = 'gradle-js-plugin'
    }
}

test {
    systemProperties['version'] = version
    testLogging {
        stackTraceFilters 'truncate', 'groovy'
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

/**
 * Create POM config and return for use by other tasks.
 */
def getPomConfiguration() {
    return {
        name 'Gradle JS Plugin'
        packaging 'jar'
        description 'A Gradle plugin for working with JS.'
        url 'https://github.com/i-net-software/gradle-js-plugin'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'eriwen'
                name 'Eric Wendelin'
                email 'me@eriwen.com'
            }
            developer {
                id 'inetsoftware'
                name 'i-net software GmbH'
                email 'contact@inetsoftware.de'
            }
        }
        scm {
            connection 'scm:https://gamma@github.com/i-net-software/gradle-js-plugin'
            developerConnection 'scm:git@github.com/i-net-software/gradle-js-plugin.git'
            url 'http://github.com/i-net-software/gradle-js-plugin'
        }
    }
}

// download dependencies all at once, keeps downloads out of travis output
task resolveAllDependencies {
    doLast {
        configurations.all { it.resolve() }
    }
}

tasks.named('compileTestGroovy') {
    dependsOn tasks.named('processTestResources')
}
